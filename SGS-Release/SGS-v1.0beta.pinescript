// kr0n0s_script_vers-1.6 , 31.dec.2024
//@version=6
// -----------------------

// ====================== //
// ====   STRATEGY   ==== //
// ====================== //

// -----------------------
strategy(title                   = "Smart Grid Strategy v1.0b",
         shorttitle              = "SGS-v1.0b",
         overlay                 = true,
         max_bars_back           = 1000,
         initial_capital         = 10000,
         default_qty_type        = strategy.fixed,
         default_qty_value       = 0.001,
         commission_type         = strategy.commission.percent,
         commission_value        = 0.035,
         calc_on_every_tick      = false,
         precision               = 0,
         slippage                = 0,
         pyramiding              = 1000,
         max_labels_count        = 500,
         close_entries_rule      = "ANY",
         max_lines_count         = 500,
         process_orders_on_close = true)
// -----------------------
//strategy.risk.max_drawdown(value = 50, type = strategy.percent_of_equity) //max risk
// -----------------------

// ========================= //
// ==== STRATEGY TIMING ==== //
// ========================= //

// -----------------------
timingX       =   true
timEnabler    =   input.bool   (false,           title = "Time limit",                                                        group = "Start Time")
startDay      =   input.int    (defval = 22,     title = "D : M : Y", step=1, minval = 1, maxval = 31,      inline ="date",   group = "Start Time")
startMonth    =   input.int    (defval = 1,     title = ":",         step=1, minval = 1, maxval = 12,      inline ="date",   group = "Start Time")
startYStr     =   input.string (defval = "2025", title = ":",         options=["2024","2025","2026","2027"],inline ="date",   group = "Start Time")
startHour     =   input.int    (defval = 6,      title = "Hrs : Min ",step=1, minval = 0, maxval = 24,      inline= "clock",  group = "Start Time") - 1 //utc adjust
startMinute   =   input.int    (defval = 6,      title = ":",         step=1, minval = 0, maxval = 59,      inline= "clock",  group = "Start Time")
//
startYear     =  2027
if startYStr  == "2026"
    startYear := 2026
if startYStr  == "2025"
    startYear := 2025
if startYStr  == "2024"
    startYear := 2024
startTime     =   timestamp    (startYear, startMonth, startDay, startHour, startMinute, 00)
if timEnabler and time < startTime 
    timingX   :=  false
// -----------------------

// ====================== //
// ====     DATA     ==== //
// ====================== //

// -----------------------
timeframe      = input.timeframe  ("1",    title = "Timeframe",                                            group = "Data"   )
src            = input.source     (close,  title = "Source",                                               group = "Data"   )
// -----------------------
step           = input.float      (0.5,    title = "Grid Step %",    minval=0.0, step=0.1,                 group = "Trading") / 100
grid_levels    = input.int        (10,      title = "Max Grids #", minval=0,   maxval=999,                  group = "Trading")

var trade_type = input.string     ("OFF",  title = "Trade Type", options=["LONG", "SHORT", "AUTO", "OFF"], group = "Trading")
decLev         = input.int        (200,    title = "Auto decision Level #", minval = 1, step = 1,          group = "Trading")

Delta          = input.bool       (false,   title = "Drawdown Reduction",                                   group = "Trading")
faK            = input.int        (10,      title = "Risk Level #", minval = 0, step = 1,                   group = "Trading")

hodl           = input.bool       (true,   title = "Profit Pump",                                          group = "Trading")
hodlquantity   = input.float      (1.5,    title = "Pump %", minval = 0.1, step = 0.1,                     group = "Trading")

Beta           = input.bool       (false,   title = "Asymmetric Risk",                                      group = "Trading")
arK            = input.int        (10,      title = "Asymmetry Factor #", minval = 1, maxval = 20,         group = "Trading") / 10

reset          = input.bool       (false,  title = "Average Reset",                                        group = "Trading")
Gamma          = input.bool       (false,  title = "Recovery Stake",                                       group = "Trading")


// Vars
src           := request.security(syminfo.tickerid, timeframe, src)
var Auto       = false
if trade_type  == "AUTO"
    Auto := true
var LastPriceL = src
var LastPriceS = src
var PyramidL   = grid_levels
var PyramidS   = grid_levels
var grid_step = step
xID           = ""
var entrypoint = 0.0
var hodlnumber = 0
var starthodl  = 0.0
var recovery1 = false
var recovery2 = false
var reduction = false
var startL    = false
var startS    = false
// Defaults
openLong      = false
openShort     = false
closeLong     = false
closeShort    = false
// -----------------------



// ====================== //
// ====   ENGINE     ==== //
// ====================== //

// ----------------------- 
AverPrice    = (strategy.position_avg_price)     // Placeholder Average Position
Long         = (strategy.position_size >  0)     // Placeholder Long Position
Short        = (strategy.position_size <  0)     // Placeholder Short Position
NoPos        = (strategy.position_size == 0)     // Placeholder No Position
if PyramidL > grid_levels or PyramidS > grid_levels
    NoPos := true
// -----------------------
// ----------------------- LONG MODE
// start grid, ex novo
if (NoPos and (trade_type == "LONG")) or startL //open long
    openLong  := true
    LastPriceL := src
    PyramidL   := grid_levels
    grid_step := step
    xID:= "L "+ str.tostring(PyramidL)
    startL := false
// extend grid, dynamic entry
if Long and (PyramidL > 0)  and (trade_type == "LONG") and (src <= LastPriceL * (1 - (grid_step*(arK)))) and Beta // long re-enter
    openLong  := true
    LastPriceL := src
    PyramidL   := PyramidL - 1
    xID:= "L "+ str.tostring(PyramidL)
    reduction := true
// extend grid, preset entry
if Long and (PyramidL > 0)  and (trade_type == "LONG") and (src <= LastPriceL * (1 - (grid_step))) and not Beta // long re-enter
    openLong  := true
    LastPriceL := src
    PyramidL   := PyramidL - 1
    xID:= "L "+ str.tostring(PyramidL)
    reduction := true
// take grid profit, fixed exit
if Long and (src >= LastPriceL * (1 + (grid_step))) and (trade_type == "LONG")  and PyramidL >= 0 //close long
    closeLong := true
    xID:= "L "+ str.tostring(PyramidL)
// -----------------------
// option: reset grid -harvest chips
if Long and reset and (PyramidL < (grid_levels - 1)) and (src >= AverPrice * (1 + (grid_step*math.sqrt(2)))) and (trade_type == "LONG")  //reset longs on Pythagoras Constant
    strategy.close_all(comment="RESET_" + str.tostring(grid_levels) + " (†" + str.tostring(math.round(strategy.max_drawdown_percent,2)) + "%)")
    recovery1 := false
    recovery2 := false
// option: drawdown recovery
if Long and PyramidL == 0 and (trade_type == "LONG") and (src <= LastPriceL * 0.90) and Gamma and not recovery1
    recovery1 := true
    openLong  := true
    xID:= "R1" 
if Long and PyramidL == 0 and (trade_type == "LONG") and (src <= LastPriceL * 0.80) and Gamma and not recovery2
    recovery2 := true
    openLong  := true
    xID:= "R2" 
// option: drawdown reduction
if reduction and PyramidL < grid_levels-faK and Delta and (trade_type == "LONG") and not openLong
    closeLong := true
    xID:= "L "+ str.tostring(PyramidL+faK+1)
    reduction := false
    PyramidL := PyramidL -1
// -----------------------
// ----------------------- SHORT MODE
// start grid, ex novo
if (NoPos and (trade_type == "SHORT")) or startS //open short
    openShort  := true
    LastPriceS := src
    PyramidS   := grid_levels
    grid_step := step
    xID:= "S "+ str.tostring(PyramidS)
    startS := false
// extend grid, dynamic entry
if Short and (PyramidS > 0)  and (trade_type == "SHORT") and (src >= LastPriceS * (1 + (grid_step*(arK)))) and Beta // short re-enter
    openShort  := true
    LastPriceS := src
    PyramidS   := PyramidS - 1
    xID:=  "S "+ str.tostring(PyramidS)
    reduction := true
// extend grid, preset entry
if Short and (PyramidS > 0)  and (trade_type == "SHORT") and (src >= LastPriceS * (1 + (grid_step))) and not Beta // short re-enter
    openShort  := true
    LastPriceS := src
    PyramidS   := PyramidS - 1
    xID:=  "S "+ str.tostring(PyramidS)
    reduction := true
// take grid profit, fixed exit
if Short and (src <= LastPriceS * (1 - (grid_step))) and (trade_type == "SHORT")  and PyramidS >= 0 //close short
    closeShort := true
    xID:=  "S "+ str.tostring(PyramidS)
// -----------------------
// option: reset grid -harvest chips
if Short and reset and (PyramidS < (grid_levels - 1)) and (src <= AverPrice * (1 - (grid_step*math.sqrt(2)))) and (trade_type == "SHORT")  //reset longs on Pythagoras Constant
    strategy.close_all(comment="RESET_" + str.tostring(grid_levels) + " (†" + str.tostring(math.round(strategy.max_drawdown_percent,2)) + "%)")
    recovery1 := false
    recovery2 := false
// option: drawdown recovery
if Short and PyramidS == 0 and (trade_type == "SHORT") and (src >= LastPriceS * 1.1) and Gamma and not recovery1
    recovery1 := true
    openShort  := true
    xID:= "R1" 
if Short and PyramidS == 0 and (trade_type == "SHORT") and (src >= LastPriceS * 1.2) and Gamma and not recovery2
    recovery2 := true
    openShort  := true
    xID:= "R2" 
// option: drawdown reduction
if reduction and PyramidS < grid_levels-faK and Delta and (trade_type == "SHORT") and not openShort
    closeShort := true
    xID:=  "S "+ str.tostring(PyramidS+faK+1)
    reduction := false
    PyramidS := PyramidS -1

// -----------------------
// ----------------------- AUTO MODE
LongSignal  = false
ShortSignal = false
// -----------------------
// Engine
activity = request.seed("seed_Kr0n-0s_tradingview","activity",close)
// Decision
if activity == 1
    LongSignal := true
if activity == 0
    ShortSignal := true
// -----------------------
// AUTO Start strategy
if Auto and (trade_type == "AUTO") and NoPos
    if LongSignal
        trade_type := "LONG"
        startL := true
    if ShortSignal
        trade_type := "SHORT"
        startS := true
// AUTO Long strategy
if Auto and (trade_type == "SHORT") and LongSignal
    trade_type := "LONG"
    strategy.close_all(comment="REVERSE/L")

// AUTO Short strategy
if Auto and (trade_type == "LONG") and ShortSignal
    trade_type := "SHORT"
    strategy.close_all(comment="REVERSE/S")

// -----------------------

// ====================== //
// ====    TRADE     ==== //
// ====================== //


// CLOSE POSITION
// -----------------------
if closeLong
    strategy.close(id = xID, comment="EXIT-" + xID + " (†" + str.tostring(math.round(strategy.max_drawdown_percent,2)) + "%)" ) //exit long
    PyramidL   := PyramidL + 1
    LastPriceL := src
if closeShort
    strategy.close(id = xID, comment="EXIT-" + xID + " (†" + str.tostring(math.round(strategy.max_drawdown_percent,2)) + "%)" ) //exit short
    PyramidS   := PyramidS + 1
    LastPriceS := src
// -----------------------

// PUMP STRATEGY
quantity = (hodlquantity/src)
// ----------------------- LONG
// Pump Profit Position
if openLong and timingX and hodl and strategy.netprofit_percent>=hodlquantity*10 and PyramidL <= (grid_levels-5) and (trade_type == "LONG")
    strategy.entry( id = "Pump-L", direction = strategy.long, qty = quantity, comment="LONG PUMP " + str.tostring(hodlnumber+1) ) //enter hodl
    entrypoint := entrypoint + src
    hodlnumber := hodlnumber + 1
    if hodlnumber == 1
        starthodl := src
// Sell Pump
if timingX and hodl and (src >= starthodl * (1 + (grid_step))) and (trade_type == "LONG")
    strategy.close( id = "Pump-L", comment="CASH L PUMP "  + str.tostring(hodlnumber) + ":" + str.tostring(math.round(entrypoint/hodlnumber,1)) )
    entrypoint := 0
    hodlnumber := 0
    starthodl  := 0
// ----------------------- SHORT
// Pump Profit Position
if openShort and timingX and hodl and strategy.netprofit_percent>=hodlquantity*10 and PyramidS <= (grid_levels-5) and (trade_type == "SHORT")
    strategy.entry( id = "Pump-S", direction = strategy.short, qty = quantity, comment="SHORT PUMP " + str.tostring(hodlnumber+1) ) //enter hodl
    entrypoint := entrypoint + src
    hodlnumber := hodlnumber + 1
    if hodlnumber == 1
        starthodl := src
// Sell Pump
if timingX and hodl and (src <= starthodl * (1 - (grid_step))) and (trade_type == "SHORT")
    strategy.close( id = "Pump-S", comment="CASH S PUMP "  + str.tostring(hodlnumber) + ":" + str.tostring(math.round(entrypoint/hodlnumber,1)) )
    entrypoint := 0
    hodlnumber := 0
    starthodl  := 0

// OPEN POSITION
// -----------------------
if openLong and timingX
    strategy.entry(id = xID, direction = strategy.long, comment="ENTER-" + xID + " :" + str.tostring(src)) //enter long
if openShort and timingX
    strategy.entry(id = xID, direction = strategy.short, comment="ENTER-" + xID + " :" + str.tostring(src)) //enter short
// -----------------------


// ====================== //
// ====     PLOT     ==== //
// ====================== //
LastPrice = src
if trade_type == "LONG"
    LastPrice := LastPriceL
if trade_type == "SHORT"
    LastPrice := LastPriceS
// -----------------------
LinePrice1 = LastPrice * (1 + grid_step)
LinePrice2 = LastPrice 
LinePrice3 = LastPrice * (1 - (grid_step))
if Beta and (trade_type == "LONG")
    LinePrice3 := LastPrice * (1 - (grid_step*(arK)))
if Beta and (trade_type == "SHORT")
    LinePrice1 := LastPrice * (1 + (grid_step*(arK)))
// -----------------------
a = plot(LinePrice1, "Price1 line", style= plot.style_line, linewidth = 2, color = color.rgb(020, 239, 009, 050))
b = plot(LinePrice2, "Price2 line", style= plot.style_line, linewidth = 1, color = color.rgb(037, 191, 229, 050))
c = plot(LinePrice3, "Price3 line", style= plot.style_line, linewidth = 2, color = color.rgb(243, 27, 7, 50))
// -----------------------
fill(a, b, color = color.rgb(8, 175, 247, 80))
fill(c, b, color = color.rgb(243, 235, 4, 80))
// -----------------------
